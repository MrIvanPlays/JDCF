plugins {
    id 'java'
    id 'maven-publish'
    id 'java-library'
}

repositories {
    mavenCentral()
    jcenter()
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 10, 'minutes'
    resolutionStrategy.cacheChangingModulesFor 10, 'minutes'
}

dependencies {
    api ('net.dv8tion:JDA:4.1.1_133')
    api ('com.fasterxml.jackson.core:jackson-databind:2.10.1')
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

group = 'com.mrivanplays'
version = '1.0.6-SNAPSHOT'
description = 'Command framework for Java Discord API'

publishing {
    repositories {
        maven {
            def releasesRepoUrl = 'https://repo.mrivanplays.com/repository/ivan-releases/'
            def snapshotsRepoUrl = 'https://repo.mrivanplays.com/repository/ivan-snapshots/'
            url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = nexusUser
                password = nexusPass
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'JDCF'
            from components.java
//            artifact sourcesJar
//            artifact javadocJar

            pom {
                name = 'JavaDiscordCommandFramework'
                description =  'Command framework for Java Discord API'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'MrIvanPlays'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/MrIvanPlays/JDCF.git'
                    developerConnection = 'scm:git:git@github.com:MrIvanPlays/JDCF.git'
                    url = 'https://github.com/MrIvanPlays/JDCF'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/MrIvanPlays/JDCF/issues'
                }
            }
        }
    }
}

compileJava.options.encoding = 'UTF-8'
